# SpaceShooter CMakeLists.txt
# Copyright (C) 2011 Alessandro Ghedini <al3xbio@gmail.com>
# This file is released under the 3 clause BSD license, see COPYING

CMAKE_MINIMUM_REQUIRED(VERSION 2.8)
PROJECT(spaceshooter C)

SET(VERSION_MAJOR 2)
SET(VERSION_MINOR 9)

SET(INSTALL_PREFIX ${CMAKE_INSTALL_PREFIX})
SET(DATA_PREFIX ${INSTALL_PREFIX}/share/games/spaceshooter)

CONFIGURE_FILE(
	${PROJECT_SOURCE_DIR}/src/config.h.in
	${PROJECT_BINARY_DIR}/src/config.h
)

INCLUDE(FindPkgConfig)
INCLUDE(CheckIncludeFiles)

PKG_CHECK_MODULES(GLFW libglfw REQUIRED)
PKG_CHECK_MODULES(SNDFILE sndfile REQUIRED)
PKG_CHECK_MODULES(OPENAL openal REQUIRED)
PKG_CHECK_MODULES(FREETYPE freetype2 REQUIRED)

FILE(GLOB SOURCES RELATIVE ${CMAKE_SOURCE_DIR} "src/*.c")

ADD_EXECUTABLE(spaceshooter ${SOURCES})

INCLUDE_DIRECTORIES(
	${CMAKE_CURRENT_BINARY_DIR}/src
	${GLFW_INCLUDE_DIRS}
	${SNDFILE_INCLUDE_DIRS}
	${OPENAL_INCLUDE_DIRS}
	${FREETYPE_INCLUDE_DIRS}
)

TARGET_LINK_LIBRARIES(
	spaceshooter
	${GLFW_LIBRARIES}
	${SNDFILE_LIBRARIES}
	${OPENAL_LIBRARIES}
	${FREETYPE_LIBRARIES}
)

SET_TARGET_PROPERTIES(spaceshooter PROPERTIES
	COMPILE_FLAGS "-Wall -pedantic -O3 -funroll-loops -ffast-math"
)

INSTALL(TARGETS spaceshooter
	DESTINATION ${CMAKE_INSTALL_PREFIX}/games
)

INSTALL(FILES
	${PROJECT_SOURCE_DIR}/man/spaceshooter.6
	DESTINATION ${CMAKE_INSTALL_PREFIX}/share/man/man6
)

INSTALL(FILES
	${PROJECT_SOURCE_DIR}/spaceshooter.desktop
	DESTINATION ${CMAKE_INSTALL_PREFIX}/share/applications
)

INSTALL(FILES
	${PROJECT_SOURCE_DIR}/data/spaceshooter.xpm
	DESTINATION ${CMAKE_INSTALL_PREFIX}/share/pixmaps
)

INSTALL(DIRECTORY data/graphics	DESTINATION ${DATA_PREFIX})
INSTALL(DIRECTORY data/sounds	DESTINATION ${DATA_PREFIX})

CONFIGURE_FILE(
	${CMAKE_CURRENT_SOURCE_DIR}/CMakeUninst.txt.in
	${CMAKE_CURRENT_BINARY_DIR}/CMakeUninst.txt
	IMMEDIATE @ONLY
)

ADD_CUSTOM_TARGET(uninstall
	COMMAND ${CMAKE_COMMAND} -P ${CMAKE_CURRENT_BINARY_DIR}/CMakeUninst.txt
)
